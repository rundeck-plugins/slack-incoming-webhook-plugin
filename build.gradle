plugins {
    id 'java'
    alias(libs.plugins.axionRelease)
    alias(libs.plugins.nexusPublish)
}

apply plugin: 'java'

group = 'org.rundeck.plugins'
ext.publishName = "Slack Incoming Webhook Plugin ${project.version}"
ext.publishDescription = project.description ?: 'Rundeck Plugin to send Slack Webhook'
ext.githubSlug = 'rundeck-plugins/slack-incoming-webhook-plugin'
ext.developers = [
        [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

java {
    sourceCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

configurations{
    //declare custom pluginLibs configuration to include only libs for this plugin
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = 'v'
        versionSeparator = ''
    }
}
project.version = scmVersion.version

repositories {
    mavenCentral()
}

dependencies {
    pluginLibs libs.freemarker
    implementation(libs.rundeckCore) {
        exclude(group: "com.jcraft")
    }

    testImplementation("org.mockito:mockito-core:5.13.0")
    testImplementation("org.slf4j:slf4j-simple:2.0.17")
    testImplementation("org.junit.jupiter:junit-jupiter:5.11.0")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation(libs.rundeckCore)
    
    // Add secure commons-lang3 to provide alternative to vulnerable commons-lang 2.6
    implementation(libs.commonsLang3)
}

configurations.all {
    resolutionStrategy {
        // Replace vulnerable commons-lang with secure commons-lang3
        dependencySubstitution {
            substitute module('commons-lang:commons-lang') using module("org.apache.commons:commons-lang3:${libs.versions.commonsLang3.get()}")
        }
    }
}

// task to copy plugin libs to output/lib dir
tasks.register('copyToLib', Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}
tasks.test {
    useJUnitPlatform()
}
jar {
    //include contents of output dir
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Name' : 'Slack Notification'
        attributes 'Rundeck-Plugin-Description' : 'Sends Rundeck notification messages to a slack channel.'
        attributes 'Rundeck-Plugin-Rundeck-Compatibility-Version': '2.8.2+'
        attributes 'Rundeck-Plugin-Tags': 'java,notification,slack'
        attributes 'Rundeck-Plugin-License': 'Apache 2.0'
        attributes 'Rundeck-Plugin-Source-Link': 'https://github.com/rundeck-plugins/slack-incoming-webhook-plugin'
        attributes 'Rundeck-Plugin-Target-Host-Compatibility': 'all'
        attributes 'Rundeck-Plugin-Author': 'Rundeck, Inc.'
        attributes 'Rundeck-Plugin-Version': '1.2', 'Rundeck-Plugin-Archive': 'true','Rundeck-Plugin-File-Version': "${project.version}"
        //create space-separated list of pluginLibs
        def libList = configurations.pluginLibs.collect{'lib/'+it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': 'com.bitplaces.rundeck.plugins.slack.SlackNotificationPlugin', 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

nexusPublishing {
    packageGroup = 'org.rundeck.plugins'
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}


apply from: "${rootDir}/gradle/publishing.gradle"